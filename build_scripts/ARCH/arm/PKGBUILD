# Maintainer: Marc McIntosh <marc@marcmcintosh.ninja>
pkgname=jxcore
pkgver=v0.3.0.6.r23.g710a754
pkgrel=1
pkgdesc="Evented IO for SpiderMonkey and V8 JavaScript"
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
url="http://jxcore.io/"
license=('common')
groups=()
depends=('openssl' 'zlib')
makedepends=('git' 'python2')
optdepends=('npm')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=('jxcore.install')
source=('git+https://github.com/jxcore/jxcore')
noextract=()
md5sums=('SKIP')
pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}
prepare(){
  cd "$srcdir/${pkgname}"
  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}
build() {
  cd "$srcdir/${pkgname}"
  [[ $CARCH == "arm" ]] && CONFIGFLAG="--with-arm-float-abi soft"
  [[ $CARCH == "armv6h" ]] && CONFIGFLAG="--with-arm-float-abi hard"
  [[ $CARCH == "armv7h" ]] && CONFIGFLAG="--with-arm-float-abi hard"
  [[ $CARCH == "aarch64" ]] && CONFIGFLAG="--with-arm-float-abi hard"
  export PYTHON=python2
  ./configure --prefix=/usr --engine-mozilla --without-npm \
    --shared-openssl --shared-zlib  $CONFIGFLAG
  make
}

package() {
  cd "$srcdir/${pkgname}"
  make DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/jxcore
  cp -r doc/api \
    "$pkgdir"/usr/share/doc/jxcore

  install -D -m644 JXCORE_LICENSE \
    "$pkgdir"/usr/share/licenses/jxcore/LICENSE
}
